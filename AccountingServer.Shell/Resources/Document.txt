控制台帮助文档

检索类命令：
[unsafe]       <记账凭证检索式>     检索记账凭证
[unsafe] fancy <记账凭证检索式>     检索记账凭证并过滤细目
[unsafe] raw   <细目映射检索式>     检索记账凭证细目（不带记账凭证信息）
[unsafe] sraw  <细目映射检索式>     检索记账凭证细目（带记账凭证信息）

汇总类命令：
     <分类汇总检索式>               执行分类汇总，显示基本结果
rps  <分类汇总检索式>               执行分类汇总，显示精确结果
Rps  <分类汇总检索式>               执行分类汇总，显示精确结果和累计
json <分类汇总检索式>               执行分类汇总，以Json显示结果
raw[(+|-)#] [@……@] <分类汇总检索式> 执行分类汇总，自动填写记账凭证
  其中：
    +    借贷不翻转（默认）
    -    借贷翻转
    #    分割记账凭证的层级，默认为0
    @……@ 需要插入记账凭证模板的内容

除了上述检索、分类汇总命令以外，还有编辑资产、编辑摊销、结转、其他、插件等命令，会在后面定义。

各种检索式的语法：

<分类汇总检索式> ::=
    | <细目映射检索式> <细目汇总参数>
    | <记账凭证检索式> <记账凭证汇总参数>

<细目汇总参数> ::=
    | `  <分类方式>* [<日期累加类型>] [<币种等值类型>] 求和，若结果为0则隐藏
    | `` <分类方式>* [<日期累加类型>] [<币种等值类型>] 求和
    | !  <分类方式>* [<日期累加类型>]                  计数
  注意：细目汇总参数内部不能有空格

<记账凭证汇总参数> ::=
    | !! <分类方式>* [<日期累加类型>]                  计数
  注意：记账凭证汇总参数内部不能有空格

<分类方式> ::=
    | v  不作分类，全部汇总
    | R  记账凭证备注
    | K  记账凭证类型
    | t  一级科目
    | s  二级科目
    | c  内容
    | r  备注
    | C  币种
    | U  记账主体
    | d  日期
    | w  周（周一开始）
    | m  自然月
    | q  季度
    | y  年
    | V  金额（注意：易受到浮点数运算误差影响）
  注意：若不写分类方式，根据所处汇总参数中的后两项决定分类方式：
    无累加、无等值：CtsUc
    无累加、有等值：tsUc
    有累加：不作分类，全部汇总

<日期累加类型> ::=
    | D             仅变动日累加
    | D[<日期范围>] 指定范围内所有日累加
    | W      范围   仅变动周累加
    | W[<日期范围>] 指定范围内所有周累加
    | M      范围   仅变动月累加
    | M[<日期范围>] 指定范围内所有月累加
    | Q      范围   仅变动季度累加
    | Q[<日期范围>] 指定范围内所有季度累加
    | Y      范围   仅变动年累加
    | Y[<日期范围>] 指定范围内所有年累加
  注意：此处的[]是字面意义上的"["和"]"，不可省略

<币种等值类型> ::=
    | X                    以汇总日汇率折算到记账本位币
    | X@<币种>             以汇总日汇率折算到指定货币
    | X[<具体日期>]        以指定日汇率折算到记账本位币
    | X@<币种>[<具体日期>] 以指定日汇率折算到指定货币
  注意：此处的[]是字面意义上的"["和"]"，不可省略

<细目映射检索式> ::=
    | <原子记账凭证检索式>            同时找到记账凭证和细目
    | <记账凭证检索式> : <细目检索式> 先找记账凭证，再找到其中符合要求的细目

<记账凭证检索式> ::=
    | <原子记账凭证检索式>
    | <原子记账凭证检索式> + <原子记账凭证检索式> 至少满足两个要求中的一个
    | <原子记账凭证检索式> - <原子记账凭证检索式> 满足前者但不满足后者
    | <原子记账凭证检索式> * <原子记账凭证检索式> 同时满足两个要求
    | { <原子记账凭证检索式> }

<原子记账凭证检索式> ::=
    | <细目检索式>   [<日期过滤器>] [^...^] [%...%[.*]] [<类型>] 任一细目符合细目检索式
    | <细目检索式> A [<日期过滤器>] [^...^] [%...%[.*]] [<类型>] 全部细目符合细目检索式
  所有条件同时满足才算匹配；其中：
    <日期过滤器> 会在后面定义
    ^...^ 记账凭证编号
    %...% 记账凭证备注，加上.*表示仅匹配前缀
    <类型> ::=
      | Ordinary     普通记账凭证
      | G|General    非结转记账凭证
      | Carry        期末结转记账凭证
      | Amortization 摊销记账凭证
      | Depreciation 折旧记账凭证
      | Devalue      减值记账凭证
      | AnnualCarry  年度结转记账凭证
      | Uncertain    不准确记账凭证

<细目检索式> ::=
    | <原子细目检索式>
    | <原子细目检索式> + <原子细目检索式> 至少满足两个要求中的一个
    | <原子细目检索式> - <原子细目检索式> 满足前者但不满足后者
    | <原子细目检索式> * <原子细目检索式> 同时满足两个要求
    | ( <原子细目检索式> )

<原子细目检索式> ::=
    | [<记账主体>] [<币种>] [<科目类型>] [<科目>] [<内容>] [<备注>] [<金额>] [<方向>]
  所有条件同时满足才算匹配；其中：
    <记账主体> ::=
      |        当前记账主体
      | U      所有记账主体
      | U...   指定记账主体
      | U'...' 指定记账主体
    <币种> ::=
      |        所有币种
      | @@     本位币
      | @...   法币/加密货币，只能是字母，自动转换为大写
      | @##@   任何法币/加密货币
      | @#...# 伪币，任意非空字符串，无自动转换
      | @#@    任何伪币
    <科目类型> ::=
      | Asset      资产[T1000,T1999]
      | Liability  负债[T2000,T2999]
      | Mutual     共同[T3000,T3999]
      | Equity     所有者权益[T4000,T4999]
      | Cost       成本[T5000,T5999]
      | Revenue    收入[T6000,T6399]+T660302
      | Expense    费用[T6400,T6999]-T660302
      | Static     静态科目（资产、负债、共同、所有者权益）
      | Dynamic    动态科目（成本、收入、费用）
      | Liquidity  流动资产[T1000,T1099]
      | Investment 投资T1101+T1441+[T1500,T1599]+[T6100,T6199]
      | Spending   消费[T1400,T1419]+T6602*(-07-11)+T6401+T6402+T6603+T1601+T1604+T1605+T1701+T671109
    <科目> ::=
      | T....   一级科目
      | T...... 一级科目和二级科目
    <内容> ::=
      | '...'   全字匹配，其中要将'换成''
      | '...'.* 前缀匹配，其中要将'换成''，不区分大小写
    <备注> ::=
      | "..."   全字匹配，其中要将"换成""
      | "...".* 前缀匹配，其中要将"换成""，不区分大小写
    <金额> ::=
      | =...    指定金额，借贷皆可
      | =+...   指定金额，借
      | =-...   指定金额，贷
      其中...为十进制浮点数，但不支持科学记数法和前缀零
    <方向> ::=
      | >       借方
      | <       贷方

<日期过滤器> ::=
    | [null]                 无日期，即无穷远之前
    | [~null]                非无日期，不是无穷远的日期
    | 具体时间点~具体时间点  两时间点（含）之间
    | 具体时间点~~具体时间点 两时间点（含）之间，或无日期
    | ~具体时间点            一时间点（含）之前，含无日期
    | ~~具体时间点           一时间点（含）之前，不含无日期
    | 具体时间点             此时间点

<具体时间点> ::=
    | yyyy       某年
    | yyyyMM     某月
    | yyyyQx     某季度
    | yyyyMMdd   某日
    | Q[1-4]     本年第1-4季度
    | Q0         本季度
    | Q-1        上季度
    | Q-2        上上季度
    ……
    | 0          本月
    | -1         上月
    | -2         上上月
    ……
    | ,          本周
    | ,,         上周
    | ,,,        上上周
    ……
    | .          今日
    | ..         昨日
    | ...        前日
    ……

编辑资产：
a 时间点? 分期检索式?                                       检索资产（以报告形式显示，无计算表，按指定日期计算账面价值，不显示账面价值为零和未购置的）
a-all 分期检索式?                                       检索资产（以报告形式显示，无计算表）
a-li 时间点? 分期检索式?                                    检索资产（以报告形式显示，有计算表，按指定日期计算账面价值，不显示账面价值为零和未购置的）
a-q 分期检索式?                                         检索资产
a-reg 分期检索式? 日期过滤器? (: 记账凭证过滤器)?           注册记账凭证
a-unreg 分期检索式? 日期过滤器? (: 记账凭证过滤器)?         解除注册记账凭证
a-recal 分期检索式?                                     重新计算折旧
a-rst-soft 分期检索式? 日期过滤器?                      解除已删除的记账凭证注册
a-rst-mixed 分期检索式? 日期过滤器? (: 记账凭证过滤器)?     删除注册了的记账凭证，并解除注册
a-rst-hard 分期检索式? (: 记账凭证过滤器)?                  删除账上所有和某资产相关的内容，但保留获得和处置
a-ap 分期检索式? 日期过滤器?                                将资产折旧/减值登记入账
a-ap col? 分期检索式? 日期过滤器?                           将资产折旧/减值登记入账，按无日期处理
a-chk 分期检索式?                                       检查一致性

编辑摊销：
o 时间点? 分期检索式?                                       检索摊销（以报告形式显示，无计算表，按指定日期计算账面价值，不显示待摊额为零和未购置的）
o-all 分期检索式?                                       检索摊销（以报告形式显示，无计算表）
o-li 时间点? 分期检索式?                                    检索摊销（以报告形式显示，有计算表，按指定日期计算账面价值，不显示待摊额为零和未购置的）
o-q 分期检索式?                                         检索摊销
o-reg 分期检索式? 日期过滤器? (: 记账凭证过滤器)?           注册记账凭证
o-unreg 分期检索式? 日期过滤器? (: 记账凭证过滤器)?         解除注册记账凭证
o-recal 分期检索式?                                     重新计算摊销
o-rst-soft 分期检索式? 日期过滤器?                      解除已删除的记账凭证注册
o-rst-mixed 分期检索式? 日期过滤器? (: 记账凭证过滤器)?     删除注册了的记账凭证，并解除注册
o-ap 分期检索式? 日期过滤器?                                将摊销登记入账
o-ap col? 分期检索式? 日期过滤器?                           将摊销登记入账，按无日期处理
o-chk 分期检索式?                                       检查一致性

分期检索式：
原子分期检索式              不进行复合
{原子分期检索式}                可通过+-*{}进行复合

原子分期检索式：
记账主体过滤器? ID过滤器? 分期名称过滤器? 分期备注过滤器? [日期过滤器]?
（注意：这里的日期过滤器不得省略[]，因此总共有两层[]）

分期名称过滤器：
/.../                       此处为正则表达式

分期备注过滤器：
%...%


结转/所有者权益币种转换命令：
ca-lst 日期过滤器?          列出记账本位币变更历史
ca-rst 日期过滤器?          删除结转/所有者权益币种转换
ca 日期过滤器?              结转/所有者权益币种转换


汇率查询命令：
?e 购汇币 金额 结汇币? 日期?        粗略查询汇率
?e-acc 购汇币 金额 结汇币? 日期?    准确查询汇率


检查命令：
chk 1                       检查基本正确性（每一记账凭证借贷方相等）
chk 2                       检查复杂正确性（每日每科目每内容，资产无贷方余额，负债无借方余额）
chk 3                       更新所有记账凭证
chk 4                       检查重复记账凭证


其他命令：
T                           检索所有科目编号及其名称
?                           帮助
explain ...                 分析表达式语法，但不执行
reload                      重新读取配置文件
die                         退出进程
version                     显示程序版本
me                          显示当前会话身份认证信息


插件命令：
$...$ ...                   调用插件
?$...$                      显示插件文档
?$$                         列出所有插件
